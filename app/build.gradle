apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.ditronic.securezipnotes"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionCode 5
        versionName "1.1.3"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Causes a "pm clear" command after each test invocation
        testInstrumentationRunnerArguments clearPackageData: 'true'

        setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashlyticsReporting: "true"]
        }
        debug {
            // A different "package name" for debug builds enables a clean separation during development.
            applicationIdSuffix = ".dev"
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashlyticsReporting: "false"]
        }
    }
    lintOptions {
        abortOnError true
        warningsAsErrors true
        lintConfig file('lint.xml')
    }
    compileOptions {
        targetCompatibility = '1.8'
        sourceCompatibility = '1.8'
        kotlinOptions.allWarningsAsErrors = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    viewBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //implementation 'com.android.support:multidex:1.0.3'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation 'androidx.biometric:biometric:1.0.1'
    //implementation 'com.google.android.gms:play-services-ads:18.0.0'
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    testImplementation 'junit:junit:4.13'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation 'androidx.test:core-ktx:1.3.0-rc01'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2-rc01'
    androidTestImplementation 'androidx.test:rules:1.3.0-rc01'
    androidTestImplementation 'androidx.test:runner:1.3.0-rc01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc01'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'

    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    implementation project(path: ':simplefilesync')
    implementation project(path: ':zip4j')
    implementation 'androidx.core:core-ktx:1.4.0-alpha01'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
